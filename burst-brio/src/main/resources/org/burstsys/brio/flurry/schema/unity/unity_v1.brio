schema Unity  {

    version : 1

    root) user : User

    structure User {
        0) id : string key                       // Flurry ID as hex string
        1) application : Application
        2) sessions : vector[Session]
        3) interests : vector[long]
        4) traits : vector[Trait]
        5) parameters : map[string, string]      // From SDK
        6) deviceModelId : long
        7) deviceSubmodelId : long
        8) deviceFormat : short                  // Enum over small set of format types
        9) estimatedAgeBucket : byte             // Enum: cfb.AgeRange, from BKD
       10) estimatedGender : byte                // Enum: cfb.Gender, from BKD
    }

    structure Application {                      // (All values latest/best from summary roll-up)
        0) id : long key
        1) channels : vector[Channel]
        2) parameters : map[string, string]      // From SDK
        3) firstUse : Use                        // First-observed values cohort
        4) lastUse : Use                         // Most-recently-observed values cohort
        5) mostUse : Use                         // Most-frequently-observed values cohort
    }

    structure Use {                              // Properties obtained/distilled at close to the same time
        0) sessionTime : long                    // Epoch time of session start
        1) appVersion : AppVersion
        2) osVersionId : long
        3) agentVersionId : long
        4) pushTokenStatus : byte                // Enum: cfbe.PushTokenStatus
        5) timeZone : string                     // See: java.time.ZoneId.getAvailableZoneIds
        6) timeZoneOffsetSecs : integer
        7) limitAdTracking : boolean             // From SDK
        8) cityId : long
        9) geoAreaId : long
       10) countryId : long
       11) regionId : long
       12) localeId : long
       13) languageId : long
       14) localeCountryId : long
       15) reportedBirthDate : long              // Epoch time
       16) reportedAgeBucket : byte              // Enum: cfb.AgeRange
       17) reportedGender : byte                 // Enum: cfb.Gender
       18) crashTime : long                      // Epoch time of most recent "crash" event
    }

    structure Session {
        0) id : long key                         // Unique ID for session, from SDK
        1) events : vector[Event]
        2) variants : vector[Variant]
        3) parameters : map[string, string]      // From SDK
        4) sessionType : byte                    // From SDK
        5) applicationUserId : string            // From SDK
        6) pushTokenStatus : byte                // Enum: cfbe.PushTokenStatus
        7) limitAdTracking : boolean             // From SDK
        8) osVersionId : long
        9) startTime : long ordinal              // Epoch time
       10) timeZone : string                     // See: java.time.ZoneId.getAvailableZoneIds
       11) cityId : long
       12) geoAreaId : long
       13) countryId : long
       14) regionId : long
       15) localeId : long
       16) carrierId : long
       17) agentVersionId : long
       18) appVersion : AppVersion
       19) duration : long                       // Units: milliseconds
       20) providedOrigin : string               // From SDK
       21) mappedOriginId: long
       22) originSourceTypeId : long
       23) originMethodTypeId : long
       24) reportedBirthDate : long              // Epoch time
       25) reportedAgeBucket : byte              // Enum: cfb.AgeRange
       26) reportedGender : byte                 // Enum: cfb.Gender
       27) reportingDelay : integer              // Units: seconds, max 30 * 86400
       28) crashed : boolean                     // True if this session contains a "crash" event
    }

    structure Event {
        0) id : long
        1) parameters : map[string, string]      // From SDK
        2) eventType : byte                      // Enum: cfbe.EventType
        3) startTime : long key ordinal          // Epoch time
        4) duration : long                       // (optional) milliseconds
        5) standardEventId : long
    }

    structure Channel {
        0) sourceId : long key
        1) parameters : map[string, string]      // From SDK
        2) campaignId : long
        3) channelId : long
    }

    structure Trait {
        0) id : long key                         // 190827 user-property value ID
        1) typeId : long                         // 190827 user-property type ID
    }

    structure Variant {
        0) id : long key
        1) versionId : long
    }

    structure AppVersion {
        0) id : long key
    }

}
